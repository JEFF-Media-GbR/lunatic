plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "io.freefair.aggregate-javadoc" version "6.5.0.3"
    id "io.freefair.aggregate-javadoc-jar" version "6.6.3"
    id 'maven-publish'
}

group = 'com.jeff_media.lunatic'
version = '1.0-SNAPSHOT'

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

jar.enabled = false

subprojects {

    tasks.register('javadocJar', Jar) {
        dependsOn javadoc
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }


    def javadocConfig = {
        options {
            links(
                    'https://docs.oracle.com/en/java/javase/16/docs/api/',
                    'https://hub.spigotmc.org/javadocs/spigot/',
                    'https://javadoc.io/doc/org.jetbrains/annotations/19.0.0'
            )
            addStringOption('Xdoclint:none', '-quiet')
        }
    }


    javadoc { configure javadocConfig }
    aggregateJavadoc { configure javadocConfig }
}

allprojects {

    apply plugin: 'java'

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }
}

dependencies {
    implementation project(':core')
    implementation project(':data-structures')
}

shadowJar {
    archiveClassifier.set('')
}

build {
    dependsOn(shadowJar, aggregateJavadoc)
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            publication.artifact(aggregateJavadocJar)
        }
    }
    repositories {
        maven {
            credentials {
                username = "jaegermeister"
                password = System.getenv("JEFF_MEDIA_REPO_SECRET")
            }
            url = 'https://repo.jeff-media.com/public/'
        }
    }
}