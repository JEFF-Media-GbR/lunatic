plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "io.freefair.aggregate-javadoc" version "6.5.0.3"
    id "io.freefair.aggregate-javadoc-jar" version "6.6.3"
    id 'maven-publish'
}

group = 'com.jeff_media.lunatic'
version = '1.7-SNAPSHOT'

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

jar.enabled = false


subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "jeff-media-public"
            url = "https://repo.jeff-media.com/public/"
        }
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }

    tasks.register('javadocJar', Jar) {
        dependsOn javadoc
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    tasks.named('test', Test) {
        useJUnitPlatform()

        maxHeapSize = '1G'

        testLogging {
            events "passed"
        }
    }


    def javadocConfig = {
        options {
            links(
                    'https://docs.oracle.com/en/java/javase/16/docs/api/',
                    'https://hub.spigotmc.org/javadocs/spigot/',
                    'https://javadoc.io/doc/org.jetbrains/annotations/19.0.0'
            )
            addStringOption('Xdoclint:none', '-quiet')
        }
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.jetbrains/annotations
        compileOnly 'org.jetbrains:annotations:24.0.1'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'

    }


    javadoc { configure javadocConfig }
    aggregateJavadoc { configure javadocConfig }

    java {
        withJavadocJar()
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }
    String moduleName = it.name

//    shadowJar {
//        archiveClassifier.set('')
//        archiveFileName.set("lunatic-" + moduleName + this.version)
//    }

    moduleName = "lunatic-" + moduleName

    publishing {
        publications {
            maven(MavenPublication) { publication ->
                groupId = this.group
                artifactId = moduleName
                version = this.version

                publication.artifact(jar)
                publication.artifact(javadocJar)
            }
        }
        repositories {
            maven {
                credentials {
                    username = "$jeffMediaPublicUser"
                    password = "$jeffMediaPublicPassword"
                }
                url = 'https://repo.jeff-media.com/public'
            }
        }
    }
}

dependencies {
    implementation project(':core')
    implementation project(':data-structures')
    implementation project(path: ':nms-1.19.4', configuration: 'reobf')
}

shadowJar {
    archiveClassifier.set('')
}

build {
    dependsOn(shadowJar, aggregateJavadoc)
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            publication.artifact(aggregateJavadocJar)
        }
    }
    repositories {
        maven {
            credentials {
                username "$jeffMediaPublicUser"
                password "$jeffMediaPublicPassword"
            }
            url = 'https://repo.jeff-media.com/public'
        }
    }
}